// vi: ft=rust

use std::str::FromStr;

use crate::ast::{
    UntypedExpr, Arg, UntypedModule, Module, UntypedStatement, Statement, TypeAst,
    UntypedPattern, BinOp, Clause, UntypedClause, RecordConstructor, Pattern, CallArg,
    ExternalFnArg, ArgNames, UnqualifiedImport, UntypedClauseGuard, ClauseGuard,
};
use crate::parser::*;

grammar;

extern {
    type Error = crate::parser::Error;
}

pub Module: UntypedModule = {
    Statement* => Module {
        type_info: (),
        name: vec![],
        statements: <>,
    }
}

Statement: UntypedStatement = {
    StatementFn => <>,
    StatementTypeAlias => <>,
    StatementCustomType => <>,
    StatementExternalFn => <>,
    StatementExternalType => <>,
    StatementImport => <>,
}

StatementTypeAlias : UntypedStatement = {
    <s:@L> <p:"pub"?> "type" <ta:TypeName> "=" <tr:Type> <e:@L> => Statement::TypeAlias {
        meta: meta(s, e),
        public: p.is_some(),
        alias: ta.0,
        args: ta.1,
        resolved_type: tr,
    }
}

StatementCustomType: UntypedStatement = {
    <s:@L> <p:"pub"?> "type" <t:TypeName> <e:@L> "{" <cs:RecordConstructor+> "}" => Statement::CustomType {
        meta: meta(s, e),
        public: p.is_some(),
        name: t.0,
        args: t.1,
        constructors: cs,
    }
}

RecordConstructor: RecordConstructor = {
    <s:@L> <t:UpName> <args:RecordConstructorArgs?> <e:@L> => RecordConstructor {
        meta: meta(s, e),
        name: t,
        args: args.unwrap_or_else(|| vec![]),
    },
}

RecordConstructorArgs: Vec<(Option<String>, TypeAst)> = {
    "(" <ts:Comma<RecordConstructorArg>> ")" => ts
}

RecordConstructorArg: (Option<String>, TypeAst) = {
    <label:(<VarName> ":")?> <typ:Type> => (label, typ),
}

StatementExternalFn: UntypedStatement = {
    <s:@L> <p:"pub"?> "external" "fn" <n:VarName> "(" <a:Comma<ExternalFnArg>> ")" "->" <r:Type> "=" <m:RawString> <f:RawString> <e:@L> => Statement::ExternalFn {
        meta: meta(s, e),
        public: p.is_some(),
        name: n,
        args: a,
        module: m,
        fun: f,
        retrn: r,
    }
}

ExternalFnArg: ExternalFnArg = {
    <label:(<VarName> ":")?> <typ:Type> => ExternalFnArg {
        label,
        typ,
    },
}

StatementFn: UntypedStatement = {
    <s:@L> <p:"pub"?> "fn" <n:VarName> "(" <a:Comma<FnArg>> ")" <return_annotation:("->" <Type>)?> "{" <b:Expr+> "}" <e:@L> => Statement::Fn {
        meta: meta(s, e),
        public: p.is_some(),
        name: n,
        args: a,
        body: seq(b),
        return_annotation,
    }
}

StatementImport: UntypedStatement = {
    "import" <s:@L> <ns:(<VarName> "/")*> <n:VarName> <e:@L> <unqualified:("." "{" <Comma<UnqualifiedImport>>"}")?> <as_name:("as" <VarName>)?> => {
        let mut module = ns;
        module.push(n);
        Statement::Import {
            meta: meta(s, e),
            unqualified: unqualified.unwrap_or_else(|| vec![]),
            module,
            as_name,
        }
    }
}

UnqualifiedImport: UnqualifiedImport = {
    <s:@L> <name:AnyName> <as_name:("as" <VarName>)?> <e:@L> => UnqualifiedImport {
        meta: meta(s, e),
        name,
        as_name
    }
}

StatementExternalType: UntypedStatement = {
    <s:@L> <p:"pub"?> "external" "type" <n:TypeName> <e:@L> => Statement::ExternalType {
        meta: meta(s, e),
        public: p.is_some(),
        name: n.0,
        args: n.1,
    }
}

pub Expr: UntypedExpr = {
    Let => <>,
    OpOrSimpleExpr => <>,
}

OpOrSimpleExpr: UntypedExpr = {
    <o:Op<OrOp, OpOrSimpleExpr, Op2>> => <>,
    Op2 => <>,
}

OrOp: BinOp = {
    "||" => BinOp::Or,
}

Op2: UntypedExpr = {
    <o:Op<AndOp, Op2, Op3>> => <>,
    Op3 => <>,
}

AndOp: BinOp = {
    "&&" => BinOp::And,
}

Op3: UntypedExpr = {
    <o:Op<EqOp, Op3, Op4>> => <>,
    Op4 => <>,
}

EqOp: BinOp = {
    "==" => BinOp::Eq,
    "!=" => BinOp::NotEq,
}

Op4: UntypedExpr = {
    <o:Op<CompareOp, Op4, Op5>> => <>,
    Op5 => <>,
}

CompareOp: BinOp = {
    "<" => BinOp::LtInt,
    "<=" => BinOp::LtEqInt,
    ">" => BinOp::GtInt,
    ">=" => BinOp::GtEqInt,
    "<." => BinOp::LtFloat,
    "<=." => BinOp::LtEqFloat,
    ">." => BinOp::GtFloat,
    ">=." => BinOp::GtEqFloat,
}

Op5: UntypedExpr = {
    <o:Op<PipeOp, Op5, Op6>> => <>,
    Op6 => <>,
}

PipeOp: BinOp = {
    "|>" => BinOp::Pipe,
}

Op6: UntypedExpr = {
    <o:Op<AddOp, Op6, Op7>> => <>,
    Op7 => <>,
}

AddOp: BinOp = {
    "+" => BinOp::AddInt,
    "+." => BinOp::AddFloat,
    "-" => BinOp::SubInt,
    "-." => BinOp::SubFloat,
}

Op7: UntypedExpr = {
    <o:Op<MultOp, Op7, SimpleExpr>> => <>,
    SimpleExpr => <>,
}

MultOp: BinOp = {
    "%" => BinOp::ModuloInt,
    "*" => BinOp::MultInt,
    "*." => BinOp::MultFloat,
    "/" => BinOp::DivInt,
    "/." => BinOp::DivFloat,
}

SimpleExpr: UntypedExpr = {
    Fn => <>,
    Nil => <>,
    Int => <>,
    Var => <>,
    Todo => <>,
    Case => <>,
    List => <>,
    Call => <>,
    Float => <>,
    Tuple => <>,
    String => <>,
    FieldAccess => <>,
    "{" <e:Expr+> "}" => seq(e),
}

Case: UntypedExpr = {
    <s:@L> "case" <subjects:CommaNoTrailing<OpOrSimpleExpr>> "{" <clauses:Clause+> "}" <e:@L> => UntypedExpr::Case {
        meta: meta(s, e),
        subjects,
        clauses,
    },
}

Clause: UntypedClause = {
    <s:@L> <pattern:Comma<Pattern>> <alternative_patterns:("|" <DelimNoTrailing<Comma<Pattern>, "|">>)?> <guard:("if" <ClauseGuard>)?> "->" <then:OpOrSimpleExpr> <e:@L> => Clause {
        meta: meta(s, e),
        pattern,
        alternative_patterns: alternative_patterns.unwrap_or_else(|| vec![]),
        guard,
        then,
    }
}

ClauseGuard: UntypedClauseGuard = {
    ClauseGuard1 => <>
}


ClauseGuard1: UntypedClauseGuard = {
    <s:@L> <left:ClauseGuard1> "||" <right:ClauseGuard2> <e:@L> => ClauseGuard::Or {
        meta: meta(s, e),
        typ: (),
        left: Box::new(left),
        right: Box::new(right),
    },

    ClauseGuard2 => <>,
}

ClauseGuard2: UntypedClauseGuard = {
    <s:@L> <left:ClauseGuard2> "&&" <right:ClauseGuard3> <e:@L> => ClauseGuard::And {
        meta: meta(s, e),
        typ: (),
        left: Box::new(left),
        right: Box::new(right),
    },

    ClauseGuard3 => <>,
}

ClauseGuard3: UntypedClauseGuard = {
    <s:@L> <left:ClauseGuard3> "==" <right:ClauseGuard4> <e:@L> => ClauseGuard::Equals {
        meta: meta(s, e),
        typ: (),
        left: Box::new(left),
        right: Box::new(right),
    },

    <s:@L> <left:ClauseGuard3> "!=" <right:ClauseGuard4> <e:@L> => ClauseGuard::NotEquals {
        meta: meta(s, e),
        typ: (),
        left: Box::new(left),
        right: Box::new(right),
    },

    ClauseGuard4 => <>,
}

ClauseGuard4: UntypedClauseGuard = {
    <s:@L> <name:VarName> <e:@L> => ClauseGuard::Var {
        meta: meta(s, e),
        typ: (),
        name,
    },

    "{" <ClauseGuard> "}" => <>,
}

Let: UntypedExpr = {
    <s:@L> "let" <p:Pattern> "=" <v:OpOrSimpleExpr> <e:@L> <t:Expr> => UntypedExpr::Let {
        meta: meta(s, e),
        value: Box::new(v),
        pattern: p,
        then: Box::new(t),
    }
}

FieldAccess: UntypedExpr = {
    <r:SimpleExpr> <s:@L> "." <label:AnyName> <e:@L> => UntypedExpr::FieldAccess {
        meta: meta(s, e),
        label,
        container: Box::new(r),
    },

    <r:SimpleExpr> <s:@L> "." <index:PositiveIntLiteral> <e:@L> => UntypedExpr::TupleIndex {
        meta: meta(s, e),
        index,
        tuple: Box::new(r),
    },
}

Fn: UntypedExpr = {
    <s:@L> "fn(" <a:Comma<FnArg>> ")" <return_annotation:("->" <Type>)?> "{" <b:Expr+> "}" <e:@L> => UntypedExpr::Fn {
        meta: meta(s, e),
        is_capture: false,
        args: a,
        body: Box::new(seq(b)),
        return_annotation,
    },
}

FnArg: Arg = {
    <s:@L> <names:ArgNames> <annotation:(":" <Type>)?> <e:@L> => Arg {
        meta: meta(s, e),
        names,
        annotation,
    },
}

ArgNames: ArgNames = {
    DiscardName => ArgNames::Discard,

    <name:VarName> => ArgNames::Named { name },

    <label:VarName> <name:VarName> => ArgNames::NamedLabelled { name, label },

    <label:VarName> DiscardName => ArgNames::LabelledDiscard { label },
}

Call: UntypedExpr = {
    <s:@L> <fun:SimpleExpr> "(" <args:Comma<CallArg>> ")" <e:@L> =>? {
        let hole_arg_name = "capture@1"; // TODO: model placeholder arguments in a less hacky way. This value can leak to the user in error messages.
        let mut num_holes = 0;
        let args = args
            .into_iter()
            .map(|a| match a {
                Ok(arg) => arg,
                Err((meta, label)) => {
                    num_holes += 1;
                    CallArg {
                        label,
                        meta: Default::default(),
                        value: UntypedExpr::Var {
                            meta,
                            name: hole_arg_name.to_string(),
                        }
                    }
                }
            })
            .collect();
        let call = UntypedExpr::Call {
            meta: meta(s, e),
            fun: Box::new(fun),
            args,
        };
        match num_holes {
            // A normal call
            0 => Ok(call),

            // An anon function using the capture syntax run(_, 1, 2)
            1 => Ok(UntypedExpr::Fn {
                meta: call.meta().clone(),
                is_capture: true,
                args: vec![Arg {
                    meta: meta(0, 0),
                    annotation: None,
                    names: ArgNames::Named{ name: hole_arg_name.to_string() },
                }],
                body: Box::new(call),
                return_annotation: None,
            }),

            count => Err(lalrpop_util::ParseError::User {
                error: Error::TooManyHolesInCapture {
                    meta: call.meta().clone(),
                    count
                },
            })
        }
    }
}

CallArg: Result<CallArg<UntypedExpr>, (crate::ast::Meta, Option<String>)> = {
    <s:@L> <label:(<VarName> ":")?> <value:OpOrSimpleExpr> <e:@L> => Ok(CallArg { label, value, meta: meta(s, e) }),
    <s:@L> <label:(<VarName> ":")?> DiscardName <e:@L> => Err((meta(s, e), label)),
}

Tuple: UntypedExpr = {
    <s:@L> "tuple" "(" <elems:Comma<OpOrSimpleExpr>> ")" <e:@L> => UntypedExpr::Tuple {
        meta: meta(s, e),
        elems: elems,
    }
}

List: UntypedExpr = {
    <s:@L> "[" <elems:Comma<OpOrSimpleExpr>> <tail:("|" <OpOrSimpleExpr>)?>"]" <e:@L> => {
        let tail = tail.unwrap_or_else(|| UntypedExpr::Nil {
            meta: meta(s, e),
        });
        elems.into_iter().rev().fold(
            tail,
            |t, h| UntypedExpr::Cons {
                meta: t.meta().clone(),
                head: Box::new(h),
                tail: Box::new(t),
            }
        )
    }
}

Var: UntypedExpr = {
    <s:@L> <v:VarName> <e:@L> => UntypedExpr::Var {
        meta: meta(s, e),
        name: v,
    },

    <s:@L> <v:UpName> <e:@L> => UntypedExpr::Var {
        meta: meta(s, e),
        name: v,
    }
}

Todo: UntypedExpr = {
    <s:@L> "todo" <e:@L> => UntypedExpr::Todo {
        meta: meta(s, e),
    }
}

Nil: UntypedExpr = {
    <s:@L> "[]" <e:@L> => UntypedExpr::Nil {
        meta: meta(s, e),
    }
}

String: UntypedExpr = {
    <s:@L> <x:RawString> <e:@L> => UntypedExpr::String {
        meta: meta(s, e),
        value: x,
    }
}

PositiveIntLiteral: u64 = {
    <pos:r"[0-9]+"> => u64::from_str(pos).unwrap(),
}

NegativeIntLiteral: i64 = {
    <neg:r"-[0-9]+"> => i64::from_str(neg).unwrap(),
}

IntLiteral: i64 = {
    <i:PositiveIntLiteral> => i as i64,
    <i:NegativeIntLiteral> => i,
}

Int: UntypedExpr = {
    <s:@L> <value:IntLiteral> <e:@L> => UntypedExpr::Int {
        meta: meta(s, e),
        value,
    }
}

Float: UntypedExpr = {
    <s:@L> <f:r"-?[0-9]+\.+[0-9]*"> <e:@L> => UntypedExpr::Float {
        meta: meta(s, e),
        value: f64::from_str(f).unwrap(),
    }
}

DelimNoTrailing<T, Delimeter>: Vec<T> = {
    <v:(<T> Delimeter)*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
}

Delim<T, Delimeter>: Vec<T> = {
    <v:(<T> Delimeter)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

Comma<T>: Vec<T> = {
    Delim<T, ","> => <>
}

CommaNoTrailing<T>: Vec<T> = {
    DelimNoTrailing<T, ","> => <>
}

Op<Operator, Current, Next>: UntypedExpr = {
    <s:@L> <l:(<Current>)> <o:(<Operator>)> <r:Next> <e:@L> => UntypedExpr::BinOp {
        meta: meta(s, e),
        name: o,
        left: Box::new(l),
        right: Box::new(r),
    },
}

Pattern: UntypedPattern = {
    PatternVar => <>,
    PatternLet => <>,
    PatternInt => <>,
    PatternNil => <>,
    PatternCons => <>,
    PatternList => <>,
    PatternFloat => <>,
    PatternString => <>,
    PatternDiscard => <>,
    PatternTuple => <>,
    PatternConstructor => <>,
}

PatternConstructor: UntypedPattern = {
    <s:@L> <module:(<VarName> ".")?> <name:UpName> <args:PatternConstructorArgs?> <e:@L> => Pattern::Constructor {
        meta: meta(s, e),
        args: args.unwrap_or_else(|| vec![]),
        constructor: (),
        module,
        name,
    },
}

PatternConstructorArgs: Vec<CallArg<UntypedPattern>> = {
    "(" <args:Comma<PatternConstructorArg>> ")" => args

}

PatternConstructorArg: CallArg<UntypedPattern> = {
    <s:@L> <label:(<VarName> ":")?> <value:Pattern> <e:@L> => CallArg {
        meta: meta(s, e),
        label,
        value,
    },
}

PatternTuple: UntypedPattern = {
    <s:@L> "tuple" "(" <elems:Comma<Pattern>> ")" <e:@L> => Pattern::Tuple {
        meta: meta(s, e),
        elems,
    }
}

PatternFloat: UntypedPattern = {
    <s:@L> <f:r"-?[0-9]+\.+[0-9]*"> <e:@L> => Pattern::Float {
        meta: meta(s, e),
        value: f64::from_str(f).unwrap(),
    }
}

PatternString: UntypedPattern = {
    <s:@L> <a:RawString> <e:@L> => Pattern::String {
        meta: meta(s, e),
        value: a,
    }
}

PatternInt: UntypedPattern = {
    <s:@L> <value:IntLiteral> <e:@L> => Pattern::Int {
        meta: meta(s, e),
        value,
    }
}

PatternLet: UntypedPattern = {
    <p:Pattern> "as" <name:VarName> => Pattern::Let {
        name,
        pattern: Box::new(p),
    }
}

PatternVar: UntypedPattern = {
    <s:@L> <v:VarName> <e:@L> => Pattern::Var {
        meta: meta(s, e),
        name: v,
    }
}

PatternDiscard: UntypedPattern = {
    <s:@L> <_d:DiscardName> <e:@L> => Pattern::Discard {
        meta: meta(s, e),
    }
}

PatternNil: UntypedPattern = {
    <s:@L> "[]" <e:@L> => Pattern::Nil {
        meta: meta(s, e),
    }
}

PatternList: UntypedPattern = {
    <s:@L> "[" <elems:Comma<Pattern>> "]" <e:@L> => {
        elems.into_iter().rev().fold(
            Pattern::Nil {
                meta: meta(e - 1, e),
            },
            |a, e| Pattern::Cons {
                meta: e.meta().clone(),
                head: Box::new(e),
                tail: Box::new(a),
            }
        )
    }
}

PatternCons: UntypedPattern = {
    <s:@L> "[" <head:Pattern> "|" <tail:Pattern> "]" <e:@L> => Pattern::Cons {
        meta: meta(s, e),
        head: Box::new(head),
        tail: Box::new(tail),
    }
}

Type: TypeAst = {
    TypeConstructor => <>,
    TypeTuple => <>,
    TypeVar => <>,
    TypeFn => <>,
}

TypeTuple: TypeAst = {
    <s:@L> "tuple" "(" <elems:Comma<Type>> ")" <e:@L> => TypeAst::Tuple {
        meta: meta(s, e),
        elems: elems,
    }
}

TypeConstructor: TypeAst = {
    <s:@L> <m:VarName> "." <n:UpName> <e:@L> => TypeAst::Constructor {
        meta: meta(s, e),
        module: Some(m),
        args: vec![],
        name: n.to_string(),
    },

    <s:@L> <m:VarName> "." <n:UpName> "(" <a:Comma<Type>> ")" <e:@L> => TypeAst::Constructor {
        meta: meta(s, e),
        module: Some(m),
        args: a,
        name: n.to_string(),
    },

    <s:@L> <n:UpName> <e:@L> => TypeAst::Constructor {
        meta: meta(s, e),
        module: None,
        args: vec![],
        name: n.to_string(),
    },

    <s:@L> <n:UpName> "(" <a:Comma<Type>> ")" <e:@L> => TypeAst::Constructor {
        meta: meta(s, e),
        module: None,
        args: a,
        name: n.to_string(),
    },
}

TypeVar: TypeAst = {
    <s:@L> <n:VarName> <e:@L> => TypeAst::Var {
        meta: meta(s, e),
        name: n,
    }
}

TypeFn: TypeAst = {
    <s:@L> "fn(" <args:Comma<Type>> ")" "->" <retrn:Type> <e:@L> => TypeAst::Fn {
        meta: meta(s, e),
        retrn: Box::new(retrn),
        args,
    }
}

TypeName: (String, Vec<String>) = {
    <v:UpName> => (v, vec![]),
    <v:UpName> "(" <a:Comma<VarName>> ")" => (v, a),
}

UpName: String = {
    <v:r"[A-Z][0-9A-Za-z]*"> => v.to_string()
}

DiscardName: () = {
    <v:r"_[A-Za-z_]*"> => ()
}

AnyName: String = {
    VarName => <>,
    UpName => <>
}

VarName: String = {
    <v:r"[a-z][0-9a-z_]*"> => v.to_string()
}

ConstructorName: String = {
    <v:r"[A-Z][0-9A-Za-z]*"> => v.to_string()
}

RawString: String = {
    <x:r#""(([^\\"]|\\.)*)""#> => x[1..(x.len() - 1)].to_string(),
}
