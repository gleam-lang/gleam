// vi: ft=rust

use std::str::FromStr;

use crate::ast::{
    UntypedExpr, UntypedArg, Arg, UntypedModule, Module, UntypedStatement, Statement, TypeAst,
    UntypedPattern, BinOp, Clause, UntypedClause, RecordConstructor, Pattern, CallArg,
    ExternalFnArg, ArgNames, UnqualifiedImport, UntypedClauseGuard, ClauseGuard, BindingKind,
    UntypedExprBinSegment, UntypedPatternBinSegment, BinSegmentOption, UntypedExprBinSegmentOption,
    UntypedPatternBinSegmentOption, Constant, UntypedConstant,
};
use crate::parser::*;

grammar;

extern {
    type Error = crate::parser::Error;
}

pub Module: UntypedModule = {
    <statements:Statement*> => Module {
        documentation: vec![],
        type_info: (),
        name: vec![],
        statements: statements,
    }
}

Statement: UntypedStatement = {
    StatementFn => <>,
    StatementTypeAlias => <>,
    StatementCustomType => <>,
    StatementExternalFn => <>,
    StatementExternalType => <>,
    StatementImport => <>,
    StatementModuleConstant => <>,
}

StatementTypeAlias : UntypedStatement = {
    <s:@L> <p:"pub"?> "type" <ta:TypeName> "=" <tr:Type> <e:@L> => Statement::TypeAlias {
        doc: None,
        location: location(s, e),
        public: p.is_some(),
        alias: ta.0,
        args: ta.1,
        resolved_type: tr,
        typ: (),
    }
}

StatementCustomType: UntypedStatement = {
    <s:@L> <p:"pub"?> <o:"opaque"?> "type" <t:TypeName> <e:@L> "{" <cs:RecordConstructor+> "}" => Statement::CustomType {
        doc: None,
        location: location(s, e),
        public: p.is_some(),
        opaque: o.is_some(),
        name: t.0,
        args: t.1,
        constructors: cs,
    }
}

RecordConstructor: RecordConstructor = {
    <s:@L> <t:UpName> <args:RecordConstructorArgs?> <e:@L> => RecordConstructor {
        location: location(s, e),
        name: t,
        args: args.unwrap_or_else(|| vec![]),
        documentation: None,
    },
}

RecordConstructorArgs: Vec<(Option<String>, TypeAst, crate::ast::SrcSpan)> = {
    "(" <ts:Comma<RecordConstructorArg>> ")" => ts
}

RecordConstructorArg: (Option<String>, TypeAst, crate::ast::SrcSpan) = {
    <s:@L> <label:(<VarName> ":")?> <typ:Type> <e:@L> => (label, typ, location(s, e)),
}

StatementExternalFn: UntypedStatement = {
    <s:@L> <p:"pub"?> "external" "fn" <n:VarName> "(" <a:Comma<ExternalFnArg>> ")" "->" <r:Type> "=" <m:RawString> <f:RawString> <e:@L> => Statement::ExternalFn {
        doc: None,
        location: location(s, e),
        public: p.is_some(),
        name: n,
        args: a,
        module: m,
        fun: f,
        retrn: r,
        return_type: (),
    }
}

ExternalFnArg: ExternalFnArg = {
    <s:@L> <label:(<VarName> ":")?> <typ:Type> <e:@L> => ExternalFnArg {
        location: location(s, e),
        label,
        typ,
    },
}

StatementFn: UntypedStatement = {
    <s:@L> <p:"pub"?> "fn" <n:VarName> "(" <a:Comma<FnArg>> ")" <return_annotation:("->" <Type>)?> <e:@L> "{" <body:ExprSequence>  <end_location:@L> "}" => Statement::Fn {
        doc: None,
        location: location(s, e - 1),
        end_location,
        public: p.is_some(),
        name: n,
        args: a,
        body,
        return_type: (),
        return_annotation,
    }
}

StatementImport: UntypedStatement = {
    "import" <s:@L> <ns:(<VarName> "/")*> <n:VarName> <e:@L> <unqualified:("." "{" <Comma<UnqualifiedImport>>"}")?> <as_name:("as" <VarName>)?> => {
        let mut module = ns;
        module.push(n);
        Statement::Import {
            location: location(s, e),
            unqualified: unqualified.unwrap_or_else(|| vec![]),
            module,
            as_name,
        }
    }
}

StatementModuleConstant: UntypedStatement = {
    <s:@L> <p:"pub"?> "const" <name:VarName> <annotation:(":" <Type>)?> "=" <lit:Constant> <e:@L> => Statement::ModuleConstant {
        doc: None,
        location: location(s, e),
        public: p.is_some(),
        name,
        annotation,
        value: Box::new(lit),
        typ: (),
    }
}

Constant: UntypedConstant = {
    ConstInt => <>,
    ConstList => <>,
    ConstFloat => <>,
    ConstTuple => <>,
    ConstString => <>,
    ConstRecord => <>,
}

ConstRecord: UntypedConstant = {
    <s:@L> <module:(<VarName> ".")?> <name:UpName> <args:ConstRecordArgs?> <e:@L> => Constant::Record {
        location: location(s, e),
        args: args.unwrap_or_else(|| vec![]),
        module,
        typ: (),
        tag: (),
        name,
    },
}

ConstRecordArgs: Vec<CallArg<UntypedConstant>> = {
    "(" <args:Comma<ConstRecordArg>> ")" => args
}

ConstRecordArg: CallArg<UntypedConstant> = {
    <s:@L> <label:(<VarName> ":")?> <value:Constant> <e:@L> => CallArg {
        location: location(s, e),
        label,
        value,
    },
}

ConstList: UntypedConstant = {
    <s:@L> "[" <elements:Comma<Constant>> "]" <e:@L> => Constant::List {
        location: location(s, e),
        elements,
        typ: (),
    },
}

ConstTuple: UntypedConstant = {
    <s:@L> "tuple" "(" <elements:Comma<Constant>> ")" <e:@L> => Constant::Tuple {
        location: location(s, e),
        elements,
    }
}

ConstInt: UntypedConstant = {
    <s:@L> <value:IntLiteral> <e:@L> => Constant::Int {
        location: location(s, e),
        value,
    }
}

ConstFloat: UntypedConstant = {
    <s:@L> <value:FloatLiteral> <e:@L> => Constant::Float {
        location: location(s, e),
        value,
    }
}

ConstString: UntypedConstant = {
    <s:@L> <x:RawString> <e:@L> => Constant::String {
        location: location(s, e),
        value: x,
    }
}

UnqualifiedImport: UnqualifiedImport = {
    <s:@L> <name:AnyName> <as_name:("as" <VarName>)?> <e:@L> => UnqualifiedImport {
        location: location(s, e),
        name,
        as_name
    }
}

StatementExternalType: UntypedStatement = {
    <s:@L> <p:"pub"?> "external" "type" <n:TypeName> <e:@L> => Statement::ExternalType {
        doc: None,
        location: location(s, e),
        public: p.is_some(),
        name: n.0,
        args: n.1,
    }
}

pub ExprSequence: UntypedExpr = {
    <s:@L> <kind:BindingKind> <p:Pattern> <annotation:(":" <Type>)?> "=" <value:OpOrSimpleExpr> <t:ExprSequence> => UntypedExpr::Let {
        location: location(s, value.location().end),
        value: Box::new(value),
        pattern: p,
        then: Box::new(t),
        annotation,
        kind,
    },

    <first:Expr> <then:ExprSequence> => UntypedExpr::Seq {
        first: Box::new(first),
        then: Box::new(then),
    },

    Expr => <>
}

pub Expr: UntypedExpr = {
    OpOrSimpleExpr => <>,
}

OpOrSimpleExpr: UntypedExpr = {
    <o:Op<OrOp, OpOrSimpleExpr, Op2>> => <>,
    Op2 => <>,
}

OrOp: BinOp = {
    "||" => BinOp::Or,
}

Op2: UntypedExpr = {
    <o:Op<AndOp, Op2, Op3>> => <>,
    Op3 => <>,
}

AndOp: BinOp = {
    "&&" => BinOp::And,
}

Op3: UntypedExpr = {
    <o:Op<EqOp, Op3, Op4>> => <>,
    Op4 => <>,
}

EqOp: BinOp = {
    "==" => BinOp::Eq,
    "!=" => BinOp::NotEq,
}

Op4: UntypedExpr = {
    <o:Op<CompareOp, Op4, Op5>> => <>,
    Op5 => <>,
}

CompareOp: BinOp = {
    "<" => BinOp::LtInt,
    "<=" => BinOp::LtEqInt,
    ">" => BinOp::GtInt,
    ">=" => BinOp::GtEqInt,
    "<." => BinOp::LtFloat,
    "<=." => BinOp::LtEqFloat,
    ">." => BinOp::GtFloat,
    ">=." => BinOp::GtEqFloat,
}

Op5: UntypedExpr = {
    <l:Op5> <s:@L> "|>" <r:Op6> <e:@L> => UntypedExpr::Pipe {
        location: location(s, e),
        left: Box::new(l),
        right: Box::new(r),
    },

    Op6 => <>,
}

Op6: UntypedExpr = {
    <o:Op<AddOp, Op6, Op7>> => <>,
    Op7 => <>,
}

AddOp: BinOp = {
    "+" => BinOp::AddInt,
    "+." => BinOp::AddFloat,
    "-" => BinOp::SubInt,
    "-." => BinOp::SubFloat,
}

Op7: UntypedExpr = {
    <o:Op<MultOp, Op7, SimpleExpr>> => <>,
    SimpleExpr => <>,
}

MultOp: BinOp = {
    "%" => BinOp::ModuloInt,
    "*" => BinOp::MultInt,
    "*." => BinOp::MultFloat,
    "/" => BinOp::DivInt,
    "/." => BinOp::DivFloat,
}

SimpleExpr: UntypedExpr = {
    Fn => <>,
    Nil => <>,
    Int => <>,
    Var => <>,
    Todo => <>,
    Case => <>,
    List => <>,
    Call => <>,
    Float => <>,
    Tuple => <>,
    String => <>,
    FieldAccess => <>,
    BitString => <>,
    "{" <expr:ExprSequence> "}" => expr,
}

Case: UntypedExpr = {
    <s:@L> "case" <subjects:CommaNoTrailing<OpOrSimpleExpr>> "{" <clauses:Clause+> "}" <e:@L> => UntypedExpr::Case {
        location: location(s, e),
        subjects,
        clauses,
    },
}

Clause: UntypedClause = {
    <s:@L> <pattern:Comma<Pattern>> <alternative_patterns:("|" <DelimNoTrailing<Comma<Pattern>, "|">>)?> <guard:("if" <ClauseGuard>)?> "->" <then:OpOrSimpleExpr> <e:@L> => Clause {
        location: location(s, e),
        pattern,
        alternative_patterns: alternative_patterns.unwrap_or_else(|| vec![]),
        guard,
        then,
    }
}

ClauseGuard: UntypedClauseGuard = {
    ClauseGuard1 => <>
}


ClauseGuard1: UntypedClauseGuard = {
    <s:@L> <left:ClauseGuard1> "||" <right:ClauseGuard2> <e:@L> => ClauseGuard::Or {
        location: location(s, e),
        left: Box::new(left),
        right: Box::new(right),
    },

    ClauseGuard2 => <>,
}

ClauseGuard2: UntypedClauseGuard = {
    <s:@L> <left:ClauseGuard2> "&&" <right:ClauseGuard3> <e:@L> => ClauseGuard::And {
        location: location(s, e),
        left: Box::new(left),
        right: Box::new(right),
    },

    ClauseGuard3 => <>,
}

ClauseGuard3: UntypedClauseGuard = {
    <s:@L> <left:ClauseGuard3> "==" <right:ClauseGuard4> <e:@L> => ClauseGuard::Equals {
        location: location(s, e),
        left: Box::new(left),
        right: Box::new(right),
    },

    <s:@L> <left:ClauseGuard3> "!=" <right:ClauseGuard4> <e:@L> => ClauseGuard::NotEquals {
        location: location(s, e),
        left: Box::new(left),
        right: Box::new(right),
    },

    ClauseGuard4 => <>,
}

ClauseGuard4: UntypedClauseGuard = {
    <s:@L> <left:ClauseGuard4> ">" <right:ClauseGuard5> <e:@L> => ClauseGuard::GtInt {
        location: location(s, e),
        left: Box::new(left),
        right: Box::new(right),
    },

    <s:@L> <left:ClauseGuard4> ">=" <right:ClauseGuard5> <e:@L> => ClauseGuard::GtEqInt {
        location: location(s, e),
        left: Box::new(left),
        right: Box::new(right),
    },

    <s:@L> <left:ClauseGuard4> "<" <right:ClauseGuard5> <e:@L> => ClauseGuard::LtInt {
        location: location(s, e),
        left: Box::new(left),
        right: Box::new(right),
    },

    <s:@L> <left:ClauseGuard4> "<=" <right:ClauseGuard5> <e:@L> => ClauseGuard::LtEqInt {
        location: location(s, e),
        left: Box::new(left),
        right: Box::new(right),
    },

    <s:@L> <left:ClauseGuard4> ">." <right:ClauseGuard5> <e:@L> => ClauseGuard::GtFloat {
        location: location(s, e),
        left: Box::new(left),
        right: Box::new(right),
    },

    <s:@L> <left:ClauseGuard4> ">=." <right:ClauseGuard5> <e:@L> => ClauseGuard::GtEqFloat {
        location: location(s, e),
        left: Box::new(left),
        right: Box::new(right),
    },

    <s:@L> <left:ClauseGuard4> "<." <right:ClauseGuard5> <e:@L> => ClauseGuard::LtFloat {
        location: location(s, e),
        left: Box::new(left),
        right: Box::new(right),
    },

    <s:@L> <left:ClauseGuard4> "<=." <right:ClauseGuard5> <e:@L> => ClauseGuard::LtEqFloat {
        location: location(s, e),
        left: Box::new(left),
        right: Box::new(right),
    },

    ClauseGuard5 => <>,
}

ClauseGuard5: UntypedClauseGuard = {
    Constant => ClauseGuard::Constant(<>),

    <s:@L> <name:VarName> <e:@L> => ClauseGuard::Var {
        location: location(s, e),
        typ: (),
        name,
    },

    "{" <ClauseGuard> "}" => <>,
}

BindingKind: BindingKind = {
    "let" => BindingKind::Let,
    "try" => BindingKind::Try,
    "assert" => BindingKind::Assert,
}

FieldAccess: UntypedExpr = {
    <r:SimpleExpr> <s:@L> "." <label:AnyName> <e:@L> => UntypedExpr::FieldAccess {
        location: location(s, e),
        label,
        container: Box::new(r),
    },

    <r:SimpleExpr> <s:@L> "." <index:PositiveRawInt> <e:@L> => UntypedExpr::TupleIndex {
        location: location(s, e),
        index,
        tuple: Box::new(r),
    },
}

Fn: UntypedExpr = {
    <s:@L> "fn(" <a:Comma<FnArg>> ")" <return_annotation:("->" <Type>)?> "{" <b:ExprSequence> "}" <e:@L> => UntypedExpr::Fn {
        location: location(s, e),
        is_capture: false,
        args: a,
        body: Box::new(b),
        return_annotation,
    },
}

FnArg: UntypedArg = {
    <s:@L> <names:ArgNames> <annotation:(":" <Type>)?> <e:@L> => Arg {
        location: location(s, e),
        typ: (),
        names,
        annotation,
    },
}

ArgNames: ArgNames = {
    <name:DiscardName> => ArgNames::Discard { name },

    <name:VarName> => ArgNames::Named { name },

    <label:VarName> <name:VarName> => ArgNames::NamedLabelled { name, label },

    <label:VarName> <name:DiscardName> => ArgNames::LabelledDiscard { label, name },
}

Call: UntypedExpr = {
    <s:@L> <fun:SimpleExpr> "(" <args:Comma<CallArg>> ")" <e:@L> =>? {
        let mut num_holes = 0;
        let args = args
            .into_iter()
            .map(|a| match a {
                Ok(arg) => arg,
                Err((location, label)) => {
                    num_holes += 1;
                    CallArg {
                        label,
                        location: Default::default(),
                        value: UntypedExpr::Var {
                            location,
                            name: crate::ast::CAPTURE_VARIABLE.to_string(),
                        }
                    }
                }
            })
            .collect();
        let call = UntypedExpr::Call {
            location: location(s, e),
            fun: Box::new(fun),
            args,
        };
        match num_holes {
            // A normal call
            0 => Ok(call),

            // An anon function using the capture syntax run(_, 1, 2)
            1 => Ok(UntypedExpr::Fn {
                location: call.location().clone(),
                is_capture: true,
                args: vec![Arg {
                    location: location(0, 0),
                    annotation: None,
                    names: ArgNames::Named { name: crate::ast::CAPTURE_VARIABLE.to_string() },
                    typ: (),
                }],
                body: Box::new(call),
                return_annotation: None,
            }),

            count => Err(lalrpop_util::ParseError::User {
                error: Error::TooManyHolesInCapture {
                    location: call.location().clone(),
                    count
                },
            })
        }
    }
}

CallArg: Result<CallArg<UntypedExpr>, (crate::ast::SrcSpan, Option<String>)> = {
    <s:@L> <label:(<VarName> ":")?> <value:OpOrSimpleExpr> <e:@L> => Ok(CallArg { label, value, location: location(s, e) }),
    <s:@L> <label:(<VarName> ":")?> DiscardName <e:@L> => Err((location(s, e), label)),
}

Tuple: UntypedExpr = {
    <s:@L> "tuple" "(" <elems:Comma<OpOrSimpleExpr>> ")" <e:@L> => UntypedExpr::Tuple {
        location: location(s, e),
        elems: elems,
    }
}

List: UntypedExpr = {
    <s:@L> "[" <elems:Comma<OpOrSimpleExpr>> <tail:(".." <OpOrSimpleExpr> ","?)?>"]" <e:@L> => {
        let tail = tail.unwrap_or_else(|| UntypedExpr::ListNil {
            location: location(s, e),
        });
        elems.into_iter().rev().fold(
            tail,
            |t, h| UntypedExpr::ListCons {
                location: t.location().clone(),
                head: Box::new(h),
                tail: Box::new(t),
            }
        )
    },
}

Var: UntypedExpr = {
    <s:@L> <v:VarName> <e:@L> => UntypedExpr::Var {
        location: location(s, e),
        name: v,
    },

    <s:@L> <v:UpName> <e:@L> => UntypedExpr::Var {
        location: location(s, e),
        name: v,
    }
}

Todo: UntypedExpr = {
    <s:@L> "todo(" <label:RawString> ")" <e:@L> => UntypedExpr::Todo {
        location: location(s, e),
        label: Some(label),
    },

    <s:@L> "todo" <e:@L> => UntypedExpr::Todo {
        location: location(s, e),
        label: None,
    }
}

BitString: UntypedExpr = {
    <s:@L> "<<" <elems:Comma<BinSegment>> ">>" <e:@L> => {
        UntypedExpr::BitString {
            location: location(s, e),
            elems: elems,
        }
    }
}

BinSegment: UntypedExprBinSegment = {
    <s:@L> <v:SimpleExpr> <options:(":" <HyphensNoTrailing<BinSegmentOption>>)?>  <e:@L> =>
        UntypedExprBinSegment {
            location: location(s, e),
            value: Box::new(v),
            typ: (),
            options: options.unwrap_or_else(|| vec![]),
        }
}

BinSegmentOption: UntypedExprBinSegmentOption = {
    <s:@L> <o:VarName> "(" <value:OpOrSimpleExpr> ")" <e:@L> => {
        if o.as_str() == "unit" {
            BinSegmentOption::Unit {
                location: location(s, e),
                value: Box::new(value),
                short_form: false
            }
        } else if o.as_str() == "size" {
            BinSegmentOption::Size {
                location: location(s, e),
                value: Box::new(value),
                short_form: false
            }
        } else {
            BinSegmentOption::Invalid { label: o, location: location(s, e) }
        }
    },

    <s:@L> <value:Int> <e:@L> =>
        BinSegmentOption::Size {
            location: location(s, e),
            value: Box::new(value),
            short_form: true
        },

    BinSegmentOptionLiteral<BinSegmentOption> => <>,
}

BinSegmentOptionLiteral<T>: T = {
    <s:@L> <o:VarName> <e:@L> => {
        match o.as_str() {
            "binary" => BinSegmentOption::Binary { location: location(s, e) },
            "bytes" => BinSegmentOption::Binary { location: location(s, e) },
            "int" => BinSegmentOption::Integer { location: location(s, e) },
            "float" => BinSegmentOption::Float { location: location(s, e) },
            "bit_string" => BinSegmentOption::BitString { location: location(s, e) },
            "bits" => BinSegmentOption::BitString { location: location(s, e) },
            "utf8" => BinSegmentOption::UTF8 { location: location(s, e) },
            "utf16" => BinSegmentOption::UTF16 { location: location(s, e) },
            "utf32" => BinSegmentOption::UTF32 { location: location(s, e) },
            "utf8_codepoint" => BinSegmentOption::UTF8Codepoint { location: location(s, e) },
            "utf16_codepoint" => BinSegmentOption::UTF16Codepoint { location: location(s, e) },
            "utf32_codepoint" => BinSegmentOption::UTF32Codepoint { location: location(s, e) },
            "signed" => BinSegmentOption::Signed { location: location(s, e) },
            "unsigned" => BinSegmentOption::Unsigned { location: location(s, e) },
            "big" => BinSegmentOption::Big { location: location(s, e) },
            "little" => BinSegmentOption::Little { location: location(s, e) },
            "native" => BinSegmentOption::Native { location: location(s, e) },
            _ => BinSegmentOption::Invalid { label: o, location: location(s, e) },
        }
    }
}

Nil: UntypedExpr = {
    <s:@L> "[]" <e:@L> => UntypedExpr::ListNil {
        location: location(s, e),
    }
}

String: UntypedExpr = {
    <s:@L> <x:RawString> <e:@L> => UntypedExpr::String {
        location: location(s, e),
        value: x,
    }
}

PositiveIntLiteral: String = {
    <pos:r"[0-9]+"> => pos.to_string()
}

NegativeIntLiteral: String = {
    <neg:r"-[0-9]+"> => neg.to_string()
}

IntLiteral: String = {
    PositiveIntLiteral => <>,
    NegativeIntLiteral => <>,
}

PositiveRawInt: u64 = {
    <pos:PositiveIntLiteral> => u64::from_str(pos.as_ref()).unwrap(),
}

Int: UntypedExpr = {
    <s:@L> <value:IntLiteral> <e:@L> => UntypedExpr::Int {
        location: location(s, e),
        value,
    }
}

FloatLiteral: String = {
    <f:r"-?[0-9]+\.[0-9]*"> => f.to_string()
}

Float: UntypedExpr = {
    <s:@L> <value:FloatLiteral> <e:@L> => UntypedExpr::Float {
        location: location(s, e),
        value
    }
}

DelimNoTrailing<T, Delimeter>: Vec<T> = {
    <v:(<T> Delimeter)*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
}

Delim<T, Delimeter>: Vec<T> = {
    <v:(<T> Delimeter)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

Comma<T>: Vec<T> = {
    Delim<T, ","> => <>
}

CommaNoTrailing<T>: Vec<T> = {
    DelimNoTrailing<T, ","> => <>
}

HyphensNoTrailing<T>: Vec<T> = {
    DelimNoTrailing<T, "-"> => <>
}

Op<Operator, Current, Next>: UntypedExpr = {
    <s:@L> <l:(<Current>)> <o:(<Operator>)> <r:Next> <e:@L> => UntypedExpr::BinOp {
        location: location(s, e),
        name: o,
        left: Box::new(l),
        right: Box::new(r),
    },
}

Pattern: UntypedPattern = {
    PatternVar => <>,
    PatternLet => <>,
    PatternInt => <>,
    PatternNil => <>,
    PatternList => <>,
    PatternFloat => <>,
    PatternString => <>,
    PatternDiscard => <>,
    PatternTuple => <>,
    PatternConstructor => <>,
    PatternBitString => <>,
}

PatternVarCallOrInt: UntypedPattern = {
    PatternVarCall => <>,
    PatternInt => <>,
}

PatternConstructor: UntypedPattern = {
    <s:@L> <module:(<VarName> ".")?> <name:UpName> <args:PatternConstructorArgs?> <e:@L> => {
        let (args, with_spread) = args.unwrap_or_else(|| (vec![], false));

        Pattern::Constructor {
            location: location(s, e),
            args: args,
            constructor: (),
            module,
            name,
            with_spread,
        }
    },
}

PatternConstructorArgs: (Vec<CallArg<UntypedPattern>>, bool) = {
    "(" <args:Comma<PatternConstructorArg>> <spread:(".."(",")?)?> ")" => (args, spread.is_some())
}

PatternConstructorArg: CallArg<UntypedPattern> = {
    <s:@L> <label:(<VarName> ":")?> <value:Pattern> <e:@L> => CallArg {
        location: location(s, e),
        label,
        value,
    },
}

PatternTuple: UntypedPattern = {
    <s:@L> "tuple" "(" <elems:Comma<Pattern>> ")" <e:@L> => Pattern::Tuple {
        location: location(s, e),
        elems,
    }
}

PatternFloat: UntypedPattern = {
    <s:@L> <value:FloatLiteral> <e:@L> => Pattern::Float {
        location: location(s, e),
        value,
    }
}

PatternString: UntypedPattern = {
    <s:@L> <a:RawString> <e:@L> => Pattern::String {
        location: location(s, e),
        value: a,
    }
}

PatternInt: UntypedPattern = {
    <s:@L> <value:IntLiteral> <e:@L> => Pattern::Int {
        location: location(s, e),
        value,
    }
}

PatternLet: UntypedPattern = {
    <p:Pattern> "as" <name:VarName> => Pattern::Let {
        name,
        pattern: Box::new(p),
    }
}

PatternVar: UntypedPattern = {
    <s:@L> <v:VarName> <e:@L> => Pattern::Var {
        location: location(s, e),
        name: v,
    }
}

PatternVarCall: UntypedPattern = {
    <s:@L> <v:VarName> <e:@L> => Pattern::VarCall {
        location: location(s, e),
        name: v,
        typ: (),
    }
}

PatternDiscard: UntypedPattern = {
    <s:@L> <name:DiscardName> <e:@L> => Pattern::Discard {
        location: location(s, e),
        name,
    }
}

PatternNil: UntypedPattern = {
    <s:@L> "[]" <e:@L> => Pattern::Nil {
        location: location(s, e),
    }
}

PatternVarOrDiscard: UntypedPattern = {
    PatternVar => <>,
    PatternDiscard => <>,
}

PatternList: UntypedPattern = {
    <s:@L> "[" <elems:Comma<Pattern>> <tail:(".." <PatternVarOrDiscard?> (",")?)?> "]" <e:@L> => {
        let tail = match tail {
            // There is a tail and it has a PatternVarOrDiscard
            Some(Some(p)) => p,

            // There is a tail and but it has no content, implicit discard
            Some(None) => Pattern::Discard {
                location: location(e - 1, e),
                name: "_".to_string()
            },

            // No tail specified
            None => Pattern::Nil {
                location: location(e - 1, e),
            },
        };

        elems.into_iter().rev().fold(
            tail,
            |a, e| Pattern::Cons {
                location: e.location().clone(),
                head: Box::new(e),
                tail: Box::new(a),
            }
        ).put_list_cons_location_start(s)
    },
}

PatternBitString: UntypedPattern = {
    <s:@L> "<<" <elems:Comma<PatternBinSegment>> ">>" <e:@L> => {
        Pattern::BitString {
            location: location(s, e),
            elems: elems,
        }
    }
}

PatternBinSegment: UntypedPatternBinSegment = {
    <s:@L> <v:Pattern> <options:(":" <HyphensNoTrailing<PatternBinSegmentOption>>)?>  <e:@L> =>
        UntypedPatternBinSegment {
            location: location(s, e),
            value: Box::new(v),
            typ: (),
            options: options.unwrap_or_else(|| vec![]),
        }
}

PatternBinSegmentOption: UntypedPatternBinSegmentOption = {
    <s:@L> <o:VarName> "(" <value:PatternVarCallOrInt> ")" <e:@L> => {
        if o.as_str() == "unit" {
            BinSegmentOption::Unit {
                location: location(s, e),
                value: Box::new(value),
                short_form: false
            }
        } else if o.as_str() == "size" {
            BinSegmentOption::Size {
                location: location(s, e),
                value: Box::new(value),
                short_form: false
            }
        } else {
            BinSegmentOption::Invalid { label: o, location: location(s, e) }
        }
    },

    <s:@L> <value:PatternInt> <e:@L> =>
        BinSegmentOption::Size {
            location: location(s, e),
            value: Box::new(value),
            short_form: true
        },

    BinSegmentOptionLiteral<PatternBinSegmentOption> => <>,
}

Type: TypeAst = {
    TypeConstructor => <>,
    TypeTuple => <>,
    TypeVar => <>,
    TypeFn => <>,
}

TypeTuple: TypeAst = {
    <s:@L> "tuple" "(" <elems:Comma<Type>> ")" <e:@L> => TypeAst::Tuple {
        location: location(s, e),
        elems: elems,
    }
}

TypeConstructor: TypeAst = {
    <s:@L> <m:VarName> "." <n:UpName> <e:@L> => TypeAst::Constructor {
        location: location(s, e),
        module: Some(m),
        args: vec![],
        name: n.to_string(),
    },

    <s:@L> <m:VarName> "." <n:UpName> "(" <a:Comma<Type>> ")" <e:@L> => TypeAst::Constructor {
        location: location(s, e),
        module: Some(m),
        args: a,
        name: n.to_string(),
    },

    <s:@L> <n:UpName> <e:@L> => TypeAst::Constructor {
        location: location(s, e),
        module: None,
        args: vec![],
        name: n.to_string(),
    },

    <s:@L> <n:UpName> "(" <a:Comma<Type>> ")" <e:@L> => TypeAst::Constructor {
        location: location(s, e),
        module: None,
        args: a,
        name: n.to_string(),
    },
}

TypeVar: TypeAst = {
    <s:@L> <n:VarName> <e:@L> => TypeAst::Var {
        location: location(s, e),
        name: n,
    }
}

TypeFn: TypeAst = {
    <s:@L> "fn(" <args:Comma<Type>> ")" "->" <retrn:Type> <e:@L> => TypeAst::Fn {
        location: location(s, e),
        retrn: Box::new(retrn),
        args,
    }
}

TypeName: (String, Vec<String>) = {
    <v:UpName> => (v, vec![]),
    <v:UpName> "(" <a:Comma<VarName>> ")" => (v, a),
}

UpName: String = {
    <v:r"[A-Z][0-9A-Za-z]*"> => v.to_string()
}

DiscardName: String = {
    <v:r"_[A-Za-z_]*"> => v.to_string()
}

AnyName: String = {
    VarName => <>,
    UpName => <>
}

VarName: String = {
    <v:r"[a-z][0-9a-z_]*"> => v.to_string()
}

ConstructorName: String = {
    <v:r"[A-Z][0-9A-Za-z]*"> => v.to_string()
}

RawString: String = {
    <x:r#""(([^\\"]|\\.)*)""#> => x[1..(x.len() - 1)].to_string(),
}
